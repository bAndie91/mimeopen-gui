#!/usr/bin/env perl

use Encode qw/encode decode/;
use Data::Dumper;
use File::MimeInfo;
use File::MimeInfo qw/mimetype_canon/;
use File::MimeInfo::Applications;
use File::MimeInfo::Magic qw/magic/;
use File::Basename qw/basename/;
use File::DesktopEntry;
use Glib qw/TRUE FALSE/;
use Gtk2 qw/-init/;
use Gtk2::SimpleList;
eval q{ use Linux::UserXAttr ':all'; };
$xattr_available = $@ ? 0 : 1;
$LF = "\n";



$MIMEOPEN_COMMAND = $0;
$0 = "mimeopen-gui";
$main_window_title = "Select application to open with...";
$screen = Gtk2::Gdk::Screen->get_default;
$win_w = 300;
$win_h = 300;



sub list_of_system_commands
{
	my %commands;
	for my $dir (split /:/, $ENV{'PATH'})
	{
		for my $file (glob "$dir/*")
		{
			if(-x $file)
			{
				$file =~ /\/([^\/]+)$/;
				my $name = $1;
				$commands{$name}=1;
			}
		}
	}
	return keys %commands;
}

$usagetext = "Usage: $0 [<File(s)> | <URI>]\n";

@Files = map { decode('utf8', $_); } @ARGV;

if(not @Files)
{
	die $usagetext;
}

$mime_type = undef;

if($Files[0] =~ /^([[:alnum:]\+\.-]+):/)
{
	if(scalar @Files > 1) { die $usagetext; }
	$uri = $Files[0];
	$mime_type = "x-scheme-handler/$1";
}
else
{
	# TODO support multi files here
	$filepath = $Files[0];
	
	if($xattr_available)
	{
		$mime_type = getxattr($filepath, 'user.mime_type');
	}
	
	if(not defined $mime_type)
	{
		if(-d $filepath)
		{
			$mime_type = "inode/directory";
		}
		elsif(-p $filepath)
		{
			$mime_type = "inode/fifo";
		}
		elsif(-S $filepath)
		{
			$mime_type = "inode/socket";
		}
		elsif(-c $filepath)
		{
			$mime_type = "inode/chardevice";
		}
		elsif(-b $filepath)
		{
			$mime_type = "inode/blockdevice";
		}
		else
		{
			$mime_type = magic($filepath) if not $mime_type;
			$mime_type = File::MimeInfo::mimetype($filepath) if not $mime_type;
			$mime_type = File::MimeInfo::default($filepath) if not $mime_type;
		}
    }
    
	($extension) = $filepath =~ /\.([^.]+)$/;
	$mime_type_by_ext = File::MimeInfo::mimetype('*.'.$extension) if $extension;
}

# TODO load apps for mimetype guessed by extension

$canonical_mime_type = mimetype_canon $mime_type;
%seen = ();

@all_canonical_apps = mime_applications_all($mime_type);
map {$_->{'mime_type'} = $canonical_mime_type if defined $_} @all_canonical_apps;
@default_nonaliased_apps = File::MimeInfo::Applications::_default($mime_type);
@other_nonaliased_apps = File::MimeInfo::Applications::_others($mime_type);
map {$_->{'mime_type'} = $mime_type if defined $_} @default_nonaliased_apps, @other_nonaliased_apps;

@default_apps = ($all_canonical_apps[0], @default_nonaliased_apps);
map {$_->{'is_default_app'} = 1 if defined $_} @default_apps;
@apps = grep {&filter_apps} @default_apps, @all_canonical_apps, @other_nonaliased_apps;



## Create Widgets
$window = new Gtk2::Window('toplevel');
$cntr0v = new Gtk2::VBox();
$cntr0h = new Gtk2::HBox();
$cntr1 = new Gtk2::VBox();
#$cntr2 = new Gtk2::HBox();
$cntr3 = new Gtk2::HBox();
$cntr3a = new Gtk2::HBox();
$lbl1scr = new Gtk2::ScrolledWindow();
$lbl2scr = new Gtk2::ScrolledWindow();
$lbl3scr = new Gtk2::ScrolledWindow();
$lbl1 = new Gtk2::Label();
$lbl2 = new Gtk2::Label();
$lbl3 = new Gtk2::Label();
$scrwin = new Gtk2::ScrolledWindow();
$slist = new Gtk2::SimpleList('icon'=>'pixbuf', 'label'=>'markup', 'is_default'=>'bool');
$slist_default_app_checkbox_colum_index = 2;
$input_cmd = new Gtk2::Entry();
$input_cmd_autocompletion = new Gtk2::EntryCompletion();
$btn_open = Gtk2::Button->new_from_stock('gtk-open');
$btn_cncl = Gtk2::Button->new_from_stock('gtk-cancel');
$btn_pref = Gtk2::Button->new_from_stock('gtk-preferences');


## Pack Widgets
$window->add($cntr0v);
$cntr0v->pack_start($cntr0h, TRUE, TRUE, 4);
$cntr0h->pack_start($cntr1, TRUE, TRUE, 4);
$cntr1->pack_start($lbl1scr, FALSE, TRUE, 0);
$lbl1scr->add_with_viewport($lbl1);
$cntr1->pack_start($lbl2scr, FALSE, TRUE, 0);
$lbl2scr->add_with_viewport($lbl2);
$cntr1->pack_start($lbl3scr, FALSE, TRUE, 0);
$lbl3scr->add_with_viewport($lbl3);
$cntr1->pack_start($scrwin, TRUE, TRUE, 0);
$cntr1->pack_start($input_cmd, FALSE, TRUE, 0);
#$cntr1->pack_start($cntr2, FALSE, TRUE, 0);
$cntr1->pack_start($cntr3, FALSE, TRUE, 0);
$scrwin->add($slist);
$cntr3->pack_start($cntr3a, TRUE, TRUE, 0);
$cntr3a->pack_start($btn_open, TRUE, FALSE, 0);
$cntr3a->pack_start($btn_cncl, TRUE, FALSE, 0);
$cntr3->pack_start($btn_pref, FALSE, FALSE, 0);


## Setup Widgets
$window->set_title($main_window_title);
$window->set_default_size($win_w, $win_h);
$window->move(($screen->get_width()-$win_w)/2, ($screen->get_height()-$win_h)/2);

$window->signal_connect('destroy' => \&main_quit);
$window->signal_connect('delete-event' => \&main_quit);
add_key_binding($window, 'Escape', \&main_quit);
add_key_binding($window, 'Return', \&btnclk_open);
add_key_binding($window, 'KP_Enter', \&btnclk_open);
$window->set_focus($slist);

$input_cmd->set_completion($input_cmd_autocompletion);
$input_cmd->signal_connect('changed', \&custom_cmd_changed);

$lbl1scr->set_policy('never', 'never');
$lbl2scr->set_policy('never', 'never');
$lbl3scr->set_policy('never', 'never');
$lbl1scr->get_children->set_shadow_type('none');
$lbl2scr->get_children->set_shadow_type('none');
$lbl3scr->get_children->set_shadow_type('none');
$lbl1scr->set_size_request($win_w - 5, -1);
$lbl2scr->set_size_request($win_w - 5, -1);
$lbl3scr->set_size_request($win_w - 5, -1);

if(defined $filepath)
{
	$lbl1->set_markup(sprintf("File: <b>%s</b>", Glib::Markup::escape_text(basename($filepath))));
	$lbl1->set_tooltip_text(Glib::Markup::escape_text($filepath));
}
else
{
	$lbl1->set_markup(sprintf("URL: <b>%s</b>", Glib::Markup::escape_text($uri)));
}
$lbl2->set_markup(sprintf("MIME type: <i>%s</i>", Glib::Markup::escape_text($mime_type)));
$lbl3->set_markup(sprintf("Canonical MIME type: <i>%s</i>", Glib::Markup::escape_text($canonical_mime_type)));

$lbl1->set_alignment(0, 0);
$lbl2->set_alignment(0, 0);
$lbl3->set_alignment(0, 0);
$lbl1->set_selectable(TRUE);
$lbl2->set_selectable(TRUE);
$lbl3->set_selectable(TRUE);
$lbl1scr->get_children->signal_connect('scroll-event' => \&scroll_viewport);
$lbl2scr->get_children->signal_connect('scroll-event' => \&scroll_viewport);
$lbl3scr->get_children->signal_connect('scroll-event' => \&scroll_viewport);

$scrwin->set_policy('automatic', 'automatic');
$slist->set_headers_visible(FALSE);
$slist->set_rules_hint(TRUE);
$slist->get_selection->signal_connect('changed', \&click_list);
$slist->signal_connect('row-activated', \&dblclk_list);
set_list_app_items($slist, @apps);

$checkbox_column = $slist->get_column($slist_default_app_checkbox_colum_index);
$toggle_cell_renderer = $checkbox_column->get_cell_renderers;
$toggle_cell_renderer->signal_connect('toggled', \&toggle_default_app_checkbox);


$btn_open->signal_connect('clicked' => \&btnclk_open);
$btn_cncl->signal_connect('clicked' => \&main_quit);
set_stock_button_label($btn_pref, '');
$btn_pref->set_tooltip_text("open config file to edit MIME type - Application assignments");
$btn_pref->signal_connect('clicked' => \&btnclk_edit);

{
	# fill input_cmd_autocompletion
	my $store = Gtk2::ListStore->new(Glib::String::);
	for my $cmd (list_of_system_commands())
	{
		$store->set($store->append, 0, $cmd);
	}
	$input_cmd_autocompletion->set_model($store);
	$input_cmd_autocompletion->set_text_column(0);
}


$custom_command_input_changed_signal_lock = 0;
$app_selection_changed_signal_lock = 0;
$CHILD = {};
$EXIT_STATUS = 0;

use POSIX ":sys_wait_h";
$SIG{'CHLD'} = sub {
	my $pid = waitpid(-1, WNOHANG);
	my $wstatus = $?;
	if(exists $CHILD->{$pid})
	{
		$CHILD->{$pid}->{'status'} = $wstatus;
	}
};


## Begin
$window->show_all();
if($canonical_mime_type eq $mime_type)
{
	$lbl3scr->hide;
}
Gtk2->main();
exit($EXIT_STATUS);


## Subroutines

sub error_message
{
	my ($msg, $msg2) = @_;
	my $dlg = new Gtk2::MessageDialog($window, 'modal', 'error', 'close', $msg);
	$dlg->set_title($main_window_title);
	$dlg->format_secondary_text($msg2) if defined $msg2;
	$dlg->run();
	$dlg->destroy();
}

sub custom_cmd_changed
{
	my ($entry) = @_;
	return 1 if $custom_command_input_changed_signal_lock;
	
	$app_selection_changed_signal_lock = 1;
	$slist->get_selection->unselect_all;
	$app_selection_changed_signal_lock = 0;
	return 1;
}

sub click_list
{
	return 1 if $app_selection_changed_signal_lock;
	
	my ($app_n) = $slist->get_selected_indices();
	my $selected_app = $apps[$app_n];
	if(defined $selected_app)
	{
		my $exec_str = $selected_app->get('Exec');
		$custom_command_input_changed_signal_lock = 1;
		$input_cmd->set_text($exec_str);
		$custom_command_input_changed_signal_lock = 0;
	}
	return 1;
}

sub toggle_default_app_checkbox
{
	my ($cellrenderer, $slist_index) = @_;
	my $is_ticked = $slist->{'data'}->[$slist_index]->[$slist_default_app_checkbox_colum_index];
	my $done = 0;
	if($slist_index < scalar @apps)
	{
		my $selected_app = $apps[$slist_index];
		my $mime_type = $selected_app->{'mime_type'};
		
		if($is_ticked)
		{
			eval { mime_applications_set_default($mime_type => $selected_app); 1 };
			$done = !$@;
		}
		else
		{
			eval { File::MimeInfo::Applications::_write_list($mime_type => ''); 1 };
			$done = !$@;
		}
		
		if($done)
		{
			if($is_ticked)
			{
				# untick all other applistions checkboxes (of the same mime type)
				for my $row_index (0 .. $#{$slist->{'data'}})
				{
					next if $row_index == $slist_index;
					next if $row_index < scalar @apps and $apps[$row_index]->{'mime_type'} ne $mime_type;
					$slist->{'data'}->[$row_index]->[$slist_default_app_checkbox_colum_index] = FALSE;
				}
			}
		}
	}
	if(!$done)
	{
		$slist->{'data'}->[$slist_index]->[$slist_default_app_checkbox_colum_index] = !$is_ticked;
	}
}

sub try_run
{
	my ($selected_app, $exe, $filepath) = @_;
	my $pid = fork;

	if(not defined $pid)
	{
		error_message("Can not fork.");
		exit 12;
	}
	elsif($pid == 0)
	{
		if(defined $selected_app) {
			$selected_app->exec($filepath);
		} else {
			exec($exe, $filepath);
		}
		exit 127;
	}
	else
	{
		$CHILD->{$pid} = {};
		if(defined $selected_app) {
			$CHILD->{$pid}->{'appname'} = $selected_app->get('Name');
		} else {
			$CHILD->{$pid}->{'appname'} = basename($exe);
		}
	}
}

sub try_run_multi
{
	my ($selected_app, $exe, @paths) = @_;
	for my $filepath (@paths)
	{
		try_run($selected_app, $exe, $filepath);
	}
	$window->hide();
	Glib::Timeout->add(300, \&check_children);
}

sub check_children
{
	for my $pid (keys %$CHILD)
	{
		if(exists $CHILD->{$pid}->{'status'})
		{
			my $wstatus = $CHILD->{$pid}->{'status'};
			my $status = $wstatus & 127;
			my $signal = $wstatus >> 8;
			if($status != 0 or $signal != 0)
			{
				error_message(sprintf("Could not run %s.", $CHILD->{$pid}->{'appname'}), "error $status, signal $signal");
			}
			delete $CHILD->{$pid};
			my $xstatus = $signal ? 128 + $signal : $status;
			$EXIT_STATUS = $xstatus if $xstatus > $EXIT_STATUS;
		}
	}
	if(not %$CHILD)
	{
		main_quit();
	}
	1;  # keep running
}

sub btnclk_open
{
	my ($app_n) = $slist->get_selected_indices();
	if(defined $app_n)
	{
		my $selected_app = $apps[$app_n];
		if(defined $selected_app)
		{
			try_run_multi($selected_app, undef, @Files)
		}
		else
		{
			my $exe;
			my $dlg = new Gtk2::FileChooserDialog("Select an Application", $window, 'open');
			$dlg->add_button('gtk-execute', 'ok');
			$dlg->add_button('gtk-cancel', 'cancel');
			if($dlg->run() eq 'ok')
			{
				$exe = $dlg->get_filename();
			}
			$dlg->destroy();
			if(defined $exe)
			{
				try_run_multi(undef, $exe, @Files);
			}
		}
	}
	else
	{
		my $exec_str = $input_cmd->get_text;
		my $basename = (split /\/+/, (split /\s+/, $exec_str)[0])[-1];
		my $desktop_entry = 
			"[Desktop Entry]".$LF.
			"Name=$basename - mimeopen-gui custom command".$LF.
			"Exec=$exec_str".$LF.
			"Type=Application".$LF.
			"StartupNotify=true";
		my $app = File::DesktopEntry->new(\$desktop_entry);
		try_run_multi($app, undef, @Files);
	}
}

sub dblclk_list
{
	my ($lst, $tpath, $tvcol) = @_;
	btnclk_open();
}

sub btnclk_edit
{
	$ENV{'MIMEOPEN_COMMAND'} = $MIMEOPEN_COMMAND;
	system('sh', '-c', 'command $MIMEOPEN_COMMAND $HOME/.local/share/applications/defaults.list &');
	main_quit();
}

sub main_quit
{
	Gtk2->main_quit;
}

sub add_key_binding
{
	my ($widget, $keyname, $sub) = @_;
	my $accelgroup = new Gtk2::AccelGroup();
	my ($key, $modifier) = Gtk2::Accelerator->parse($keyname);
	$accelgroup->connect($key, $modifier, 'visible', $sub);
	$widget->add_accel_group($accelgroup);
}

sub set_stock_button_label
{
	my ($wdg, $text) = @_;
	my ($a) = $wdg->get_children; # Alignment
	my ($a) = $a->get_children; # HBox
	my ($i, $l) = $a->get_children;
	$l->set_label($text);
}

sub icondir2size
{
	return(($_[0] =~ /\/(\d+)x\d+/)[0]);
}

sub pango_escape
{
	my ($s) = @_;
	$s =~ s/</&lt;/g;
	$s =~ s/>/&gt;/g;
	$s =~ s/&/&amp;/g;
	return $s;
}

sub set_list_app_items
{
	my ($slist, @apps) = @_;
	my $slist_data = $slist->{'data'};
	my $pref_size = 24;
	my $select_index_by_default = undef;
	my $index = -1;
	
	for my $app (@apps)
	{
		$index ++;
		my $pxb;
		my $iconname = $app->get('Icon');
		my $appname = $app->get('Name');
		if($iconname)
		{
			my $iconpath;
			if($iconname =~ /^\//)
			{
				$iconpath = $iconname;
			}
			else
			{
				my @dirs;
				for my $c ('hicolor', 'locolor', 'gnome')
				{
					my $d = "/usr/share/icons/${c}";
					my @small;
					for my $e (sort {icondir2size($a) <=> icondir2size($b)} glob "$d/*x*/*")
					{
						if(icondir2size($e) < $pref_size)
						{
							unshift @small, $e;
						}
						else
						{
							push @dirs, $e;
						}
					}
					push @dirs, "$d/scalable/*";
					for my $e (@small)
					{
						push @dirs, $e;
					}
				}
				push @dirs, '/usr/share/pixmaps';
				
				FINDICON:
				for my $icondir (@dirs)
				{
					my $iconpath_test;
					for my $iconext ('png', 'xpm', 'svg')
					{
						if($iconname =~ /\./)
						{
							$iconpath_test = $icondir . '/'. $iconname;
						}
						else
						{
							$iconpath_test = $icondir . '/'. $iconname . '.' . $iconext;
						}
						if(-e $iconpath_test)
						{
							$iconpath = $iconpath_test;
							last FINDICON;
						}
						elsif($iconname =~ /\./)
						{
							next FINDICON;
						}
					}
				}
			}
			if(defined $iconpath)
			{
				eval {
					$pxb = Gtk2::Gdk::Pixbuf->new_from_file_at_scale($iconpath, $pref_size, -1, TRUE);
					1;
				}
				or $pxb = undef;
			}
		}
		
		$select_index_by_default = $index if not defined $select_index_by_default and $app->{'is_default_app'};
		push @$slist_data, [$pxb, sprintf("<big>%s</big>\n<small>%s</small>", pango_escape($appname), pango_escape($app->get('Exec'))), $app->{'is_default_app'} ? TRUE : FALSE];
	}
	
	$slist->select($select_index_by_default || 0);
	push @$slist_data, [Gtk2::Button->new->render_icon('gtk-add', 'button'), '<big>'."Custom application...".'</big>', FALSE];
}

sub filter_apps
{
	my $a = $_;
	return 0 if !defined $a;
	my $x = $a->get('Exec');
	my $had_seen = $seen{$x};
	$seen{$x} = 1;
	return !$had_seen;
}

sub scroll_viewport
{
	my ($self, $event) = @_;
	my $scrwin = $self->parent;
	my $dir = $event->direction;
	my $adj = $scrwin->get_hadjustment;
	my $delta = ($dir eq 'down' or $dir eq 'left') ? +10 : -10;
	my $new = $adj->value + $delta;
	my $width = $self->get_allocation->width;
	$new = $adj->upper - $width if $new + $width > $adj->upper;
	$adj->set_value($new);
}
